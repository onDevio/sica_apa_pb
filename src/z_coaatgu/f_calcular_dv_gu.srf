HA$PBExportHeader$f_calcular_dv_gu.srf
$PBExportComments$Calcula el total de dv para Guadalajara
global type f_calcular_dv_gu from function_object
end type

forward prototypes
global function integer f_calcular_dv_gu (ref st_dv_datos st_dv_datos)
end prototypes

global function integer f_calcular_dv_gu (ref st_dv_datos st_dv_datos);string Tarifa, Contenido
double importe_total=0, Pres, IPC

// Si es obra de la administracion no se cobran los derechos de visado
if st_dv_datos.administracion then 
	st_dv_datos.dv = 0
	return 0
end if

if isnull(st_dv_datos.porcentaje) then st_dv_datos.porcentaje  = 100

Tarifa 			= st_dv_datos.tarifa
Contenido 		= st_dv_datos.contenido
Pres 				= st_dv_datos.pem

// Para la tarifa de seguridad se utiliza el presupuesto de seguridad
if Tarifa = '5.15' then pres = st_dv_datos.pres_seguridad

if isnull(Contenido) or Contenido='' then Contenido = '@'

if Pres > 3000000 then
	// Por cada 60000 euros o fracci$$HEX1$$f300$$ENDHEX$$n que superen los 3 millones de presup. se aumentar$$HEX1$$e100$$ENDHEX$$n 15 euros a 510 euros
	double exc, fracc
	exc = Pres - 3000000
	fracc = ceiling(exc/60000)
	importe_total = 510 + (15 * fracc)
else
	SELECT importe INTO :importe_total FROM dv_sobre_pem WHERE pem_desde <= :Pres AND pem_hasta >= :Pres ;
end if

// Coeficiente Ca, tomamos Cp como el IPC
SELECT desc_gui_coefs.coef_p
INTO :IPC
FROM desc_gui_coefs  
WHERE ( desc_gui_coefs.desde_fecha <= :st_dv_datos.fecha ) AND  
		( desc_gui_coefs.hasta_fecha > :st_dv_datos.fecha )   ;


CHOOSE CASE Tarifa
	CASE '3.1', '3.2', '3.3', '3.4', '4.1.1', '4.1.2', '4.1.4', 'A.2.5.1', 'OT'
			importe_total = 20 * IPC
			
	CASE 'A.1.1', 'A.1.2', 'A.1.3', 'A.1.4', 'A.1.6.3', 'A.1.6.4'
			importe_total += 30 
			
	CASE '5.15'
		CHOOSE CASE Contenido
			CASE '440', '443', '445', '446', '447'
				importe_total += 100
			// Modificado Paco 31/10/2005. Para coordinaciones los Dv son el m$$HEX1$$e100$$ENDHEX$$ximo entre el resultado de la tabla y el m$$HEX1$$ed00$$ENDHEX$$nimo que es 20 euros
			CASE '442','448'
				importe_total = max(importe_total, 20*IPC)
			CASE ELSE
				importe_total = 20 * IPC
		END CHOOSE
END CHOOSE

importe_total = f_redondea(importe_total * st_dv_datos.porcentaje / 100 )

if st_dv_datos.visared = 'V' then
	importe_total = f_redondea( importe_total * (1 - g_porc_dv_descuento_visared))
end if

st_dv_datos.dv = importe_total

return 1

end function

